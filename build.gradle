plugins {
	id "org.jetbrains.intellij" version "1.6.0"
	id "org.jetbrains.kotlin.jvm" version "1.7.21"
    id "java"
	id 'org.jetbrains.kotlin.plugin.serialization' version '1.7.21'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'joda-time:joda-time:2.12.1'
    implementation "org.apache.commons:commons-csv:1.9.0"
	implementation 'com.clickhouse:clickhouse-client:0.3.2'
	implementation 'com.clickhouse:clickhouse-http-client:0.3.2'
	testImplementation "junit:junit:4.13.2" // also includes hamcrest-core
	implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1'
}

sourceSets {
	main {
		kotlin { srcDir "./src/main" }
		resources { srcDir "./src/resources" }
	}
	test {
		kotlin { srcDir "./src/test" }
	}
}

compileKotlin {
	kotlinOptions {
		apiVersion = "1.6"
		languageVersion = "1.6"
        // Compiler flag to allow building against pre-released versions of Kotlin
        // because IJ EAP can be built using pre-released Kotlin but it's still worth doing to check API compatibility
        freeCompilerArgs = ["-Xskip-metadata-version-check"] 
    }
}

intellij {
	// To find available IDE versions see https://www.jetbrains.com/intellij-repository/releases
	version = System.getenv().getOrDefault("IJ_VERSION",
        "221.5080.210" // Compatibility with IJ 2022.1
//			"LATEST-EAP-SNAPSHOT"
    )
	// The artifact name is deliberately not "activity-tracker" because
	// *.csv files are stored in the "activity-tracker" directory.
	// If plugin archive has the same name, the directory will be deleted on plugin update.
	pluginName = "activity-tracker-plugin"
    plugins = ["java", "tasks"]
	downloadSources = true
    updateSinceUntilBuild = false // Disable patching plugin.xml because "until" version is too restrictive (it's better to keep it open-ended).
}
